plugins {
    id 'java'
    alias(libs.plugins.spring.boot) apply false
    alias(libs.plugins.spring.dependency.management) apply false
    alias(libs.plugins.spotless)
    alias(libs.plugins.git.properties)
    alias(libs.plugins.ben.manes)

}

// extra properties
ext {
    defaultEncoding = 'UTF-8'
    javaVersion = JavaVersion.VERSION_25.toString()
}

allprojects {

    group = 'com.paravar.retailflow'
    version = project.hasProperty('BUILD_SOURCEVERSION') ?  project.property('BUILD_SOURCEVERSION') : System.getenv('BUILD_SOURCEVERSION') ?: '0.0.1'
    description = 'Production grade microservices'

    repositories {
        mavenCentral()
    }
}

subprojects {Project sub->

    // Only configure projects that contain src/main/java
    boolean hasSource = file('src/main/java').exists() && !fileTree('src/main/java').include('**/*.java').isEmpty()

    if (!hasSource) {
        logger.lifecycle("Skipping empty project: ${sub.name}")
        return
    }
    else{
        logger.lifecycle("Configuring the project: ${sub.name}")
    }

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'com.gorylenko.gradle-git-properties'
    apply plugin: 'com.github.ben-manes.versions'

    java {
        toolchain {
            languageVersion =  JavaLanguageVersion.of(javaVersion)
        }

    }

        tasks.withType(JavaCompile).configureEach {
            options.encoding = defaultEncoding

            // pass mapstruct compile args conditionally
            if (configurations.annotationProcessor.dependencies.any { it.name == "mapstruct" }) {
                options.compilerArgs += [
                        '-Amapstruct.suppressGeneratorTimestamp=true',
                        '-Amapstruct.suppressGeneratorVersionInfoComment=true',
                        '-Amapstruct.defaultComponentModel=spring'
                ]
            }
        }

    dependencies {
        compileOnly libs.lombok
        annotationProcessor libs.lombok

        implementation libs.mapstruct
        annotationProcessor libs.mapstruct

        compileOnly libs.spring.boot.configuration.processor
        annotationProcessor libs.spring.boot.configuration.processor

        testImplementation libs.spring.boot.starter.test

    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    // tasks.named('test')
    test {
        useJUnitPlatform()
        testLogging {
            events = ["PASSED", "FAILED", "SKIPPED"]
            showStandardStreams = true
            exceptionFormat = "full"
        }
    }

}
spotless {
    java {
        googleJavaFormat()
        cleanthat() // modifies code logic (not just formatting). be careful
        importOrder()
        removeUnusedImports()
        formatAnnotations()
        trimTrailingWhitespace()
        endWithNewline()
    }
}


gitProperties {
    keys = ['git.branch','git.commit.user.name', 'git.commit.id', 'git.commit.time']
    force = true
}
tasks.register('spotlessApplyAll') {
    dependsOn subprojects.findResults { it.tasks.findByName('spotlessApply') }
}

tasks.register('spotlessCheckAll') {
    dependsOn subprojects.findResults { it.tasks.findByName('spotlessCheck') }
}

// refer: product-catalog-service for project level build config